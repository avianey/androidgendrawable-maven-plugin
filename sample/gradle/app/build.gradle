apply plugin: 'com.android.application'
apply plugin: "androidsvgdrawable"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "fr.avianey.androidsvgdrawable.sample"
        minSdkVersion 9
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    productFlavors {
        flavor1 {
            applicationId "fr.avianey.androidsvgdrawable.sample.flavor1"
        }
    }

    sourceSets{
        main.res.srcDirs = [main.res.srcDirs, "build/generated/res/main"]
        flavor1.res.srcDirs = [flavor1.res.srcDirs, "build/generated/res/flavor1"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

// create a task to convert SVG to PNG DRAWABLE
task svgToDrawablePng(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    // specify where to pick SVG from
    from = file('src/main/svg-png')
    // specify the android res folder
    to = file('build/generated/res/main')
    // create qualified directories if missing
    createMissingDirectories = true
    // override files only if necessary
    overrideMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['ldpi', 'hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // relative path of the file specifying nine patch specs
    ninePatchConfig = file('src/main/svg-png/9patch.json')
    // output format of the generated resources
    outputFormat = 'PNG'

    // this is an experimental feature
    // it allow composition of SVG two generate resources for multiple combinations
    svgMaskDirectory = file('src/main/svg-png/mask')
    svgMaskResourcesDirectory = file('src/main/svg-png/mask/svg')

    // svg resources can be renamed on the fly
    rename = [phone_to_rename: 'phone']
}

// create a task to convert SVG to PNG MIPMAP
task svgToMipmapPng(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    // specify where to pick SVG from
    from = file('src/main/svg-mipmap')
    // specify the android res folder
    to = file('build/generated/res/main')
    // create qualified directories if missing
    createMissingDirectories = true
    // override files only if necessary
    overrideMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['ldpi', 'hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // output format of the generated resources
    outputFormat = 'PNG'
    // output type is mipmap
    outputType = 'mipmap'

    // the icon svg file will be used to generate a google play high res (512x512) icon
    highResIcon = 'icon'
}

// multiple task can be used
task svgToDrawableJpg(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = file('src/main/svg-jpg')
    to = file('build/generated/res/main')
    createMissingDirectories = true
    overrideMode = 'ifModified'
    targetedDensities = ['ldpi', 'mdpi', 'hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'JPG'
}

// flavor specific
task svgToMipmapPngFlavor1(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    // specify where to pick SVG from
    from = file('src/main/svg-mipmap-flavor1')
    // specify the android res folder
    to = file('build/generated/res/flavor1')
    // let generate PNG for the following densities only
    targetedDensities = ['ldpi', 'hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // output format of the generated resources
    outputFormat = 'PNG'
    // output type is mipmap
    outputType = 'mipmap'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
